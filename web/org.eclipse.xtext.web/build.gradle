plugins {
  id "com.eriwen.gradle.js" version "1.12.1"
}

dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile project(':plugins:org.eclipse.xtext.ide')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile project(':plugins:org.eclipse.xtext.junit4')
    testCompile project(':web:org.eclipse.xtext.web.example.statemachine')
    testCompile project(':web:org.eclipse.xtext.web.example.statemachine.ide')
}

javascript.source {
	main.js {
    	srcDir 'src/main/js'
	}
}

def requireJsPlugins = file("$buildDir/tmp/js/requirejs-plugins")
task downloadRequireJsPlugins {
	onlyIf {!requireJsPlugins.exists()}
	doLast {
		requireJsPlugins.mkdirs()
		ant.get(src: 'https://raw.github.com/requirejs/i18n/latest/i18n.js', dest: requireJsPlugins.path + "/i18n.js")
		ant.get(src: 'https://raw.github.com/requirejs/text/latest/text.js', dest: requireJsPlugins.path + "/text.js")
	}
}

requirejs.buildprofile = file('src/main/js/requirejs-config.js')
requirejs.options = [optimize: 'none']

requireJs {
	dependsOn(downloadRequireJsPlugins)
    source = javascript.source.main.js
    dest = file("$buildDir/opt-resources/META-INF/resources/xtext/${project.version}/xtext.js")
}

task copyCss(type: Copy) {
    from 'src/main/js/orion/built-editor.css'
    into "$buildDir/opt-resources/META-INF/resources/xtext/${project.version}"
    rename { 'xtext.css' }
}

sourceSets.main.resources.srcDir "$buildDir/opt-resources"
processResources.dependsOn(requireJs, copyCss)
